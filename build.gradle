buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.badlogicgames.gdx:gdx-jnigen-gradle:2.3.1"
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "com.bearwaves"
version = "1.0.0"

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:1.11.0"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
}

sourceSets {
    main {
        resources {
            srcDirs = [
                    "libs/macosx64",
                    "libs/macosxarm64",
                    "libs/windows64",
                    "libs/linux64",
            ]
        }
    }
}

tasks.named('test') {
    sourceSets.test.resources.srcDirs += "jni/EOS/SDK/Bin"

    def localProperties = new Properties()
    def propertiesFile = rootProject.file("local.properties")
    if (propertiesFile.exists()) {
        localProperties.load(propertiesFile.newDataInputStream())
    }
    environment "EOS4J_TEST_PRODUCT_ID", localProperties.getProperty("EOS4J_TEST_PRODUCT_ID") ?: System.getenv("EOS4J_TEST_PRODUCT_ID")
    environment "EOS4J_TEST_SANDBOX_ID", localProperties.getProperty("EOS4J_TEST_SANDBOX_ID") ?: System.getenv("EOS4J_TEST_SANDBOX_ID")
    environment "EOS4J_TEST_DEPLOYMENT_ID", localProperties.getProperty("EOS4J_TEST_DEPLOYMENT_ID") ?: System.getenv("EOS4J_TEST_DEPLOYMENT_ID")
    environment "EOS4J_TEST_CLIENT_ID", localProperties.getProperty("EOS4J_TEST_CLIENT_ID") ?: System.getenv("EOS4J_TEST_CLIENT_ID")
    environment "EOS4J_TEST_CLIENT_SECRET", localProperties.getProperty("EOS4J_TEST_CLIENT_SECRET") ?: System.getenv("EOS4J_TEST_CLIENT_SECRET")

    useJUnitPlatform()
}

apply plugin: "com.badlogicgames.gdx.gdx-jnigen"
jnigen {
    sharedLibName = "eos4j"

    nativeCodeGenerator {
        sourceDir = "src/main/java"
    }

    all {
        headerDirs = ["EOS/SDK/Include", "cpp"]
        cppFlags += " -std=c++17 "
        cppExcludes += "EOS/**"
    }

    add(Windows, x64) {
        libraries += "-L" + file("./jni/EOS/SDK/Lib").absolutePath + " -lEOSSDK-Win64-Shipping"
    }
    add(MacOsX, x64) {
        libraries += "-L" + file("./jni/EOS/SDK/Bin").absolutePath + " -lEOSSDK-Mac-Shipping"
    }
    add(MacOsX, x64, ARM) {
        libraries += "-L" + file("./jni/EOS/SDK/Bin").absolutePath + " -lEOSSDK-Mac-Shipping"
    }
    add(Linux, x64) {
        libraries += "-L" + file("./jni/EOS/SDK/Bin").absolutePath + " -lEOSSDK-Linux-Shipping"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "eos4j"
            from components.java

            pom {
                name = "eos4j"
                description = "Java wrapper for Epic Online Services SDK"
                url = "https://github.com/Bearwaves/eos4j"

                developers {
                    developer {
                        id = 'JoelOtter'
                        name = 'Joel Auterson'
                        email = 'joel@bearwaves.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials)
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.maven
}